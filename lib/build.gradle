plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlinx-serialization'
    id 'maven-publish'
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 4
        versionName "1.3.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        // read RPC URL properties fro testing
        def defaultRpcUrl = project.properties["testing.rpc.defaultUrl"]
        def rpcUrl = project.properties["rpcUrl"] ?: defaultRpcUrl

        def useLocalValidator = project.properties["localValidator"].toBoolean()
        def localRpcUrl = project.properties["testing.rpc.localUrl"]
        if (useLocalValidator && localRpcUrl != null) rpcUrl = localRpcUrl

        // set RPC URL (used in testing)
        buildConfigField 'String', 'RPC_URL', "\"${rpcUrl.toString().replace('"', '')}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {

    testImplementation 'junit:junit:4.+'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.8'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.0'

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0'

    // SolanaKT
    implementation 'com.github.metaplex-foundation:SolanaKT:2.0.0'

    // Borsh
    implementation ('com.github.metaplex-foundation:kborsh:0.1.0b1'){
        exclude group: 'com.github.metaplex-foundation.kborsh', module:'kborsh-jvm'
    }

    // Moshi
    implementation "com.squareup.moshi:moshi:1.13.0"
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.13.0'

    implementation 'com.squareup:kotlinpoet:1.0.0'
}

//region PUBLISHING
group 'com.metaplex'
version android.defaultConfig.versionName

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact("$buildDir/outputs/aar/lib-release.aar")  {
                builtBy tasks.getByName("assemble")
            }
            artifactId 'metaplex'
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/metaplex-foundation/metaplex-android"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
//endregion